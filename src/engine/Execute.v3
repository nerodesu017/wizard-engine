// Copyright 2022 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// The {Execute} component provides an all-purpose facade to the rest of the engine and runtime
// that deals with execution, modes, and stack traces. It separates the runtime from specifics
// of the target, which may have one or more execution tiers.
component Execute {
	var limits = Limits.new();		// limits on execution/memory
	var exit_code: int;
	var handleTrap = defaultTrap;		// how to handle traps
	var tiering: ExecutionStrategy;
	def trapObjects = Array<Trap>.new(TrapReason.count);
	def NO_FRAMES = Array<(WasmFunction, int)>.new(0);

	new() {
		for (r in TrapReason) trapObjects[r.tag] = Trap.new(r, null, null);
	}

	// Reset internal states.
	def reset() {
		Instrumentation.reset();
		tiering.reset();
	}
	// Call a given function with the given arguments.
	def call(func: Function, args: Range<Value>) -> Result {
		return tiering.call(func, args);
	}
	def funcSigMismatch() -> HostResult.Throw {
		return HostResult.Throw(Trap.new(TrapReason.FUNC_SIG_MISMATCH, null, null));
	}
	def trap(reason: TrapReason, hf: HostFunction) -> Result.Throw {
		var segment = if(hf != null, StackSegment.new(null, hf, NO_FRAMES));
		return Result.Throw(Trap.new(reason, null, segment));
	}

	// Traces calls to host functions.
	def traceCallHostFunction(hf: HostFunction) {
//TODO		var c = getCallStack();
//TODO		for (i < c.top) Trace.OUT.sp();
		Trace.OUT.put2("callHost(\"%q\": %q)", hf.render, hf.sig.render).outln();
	}

	def fwBoundCall(f: Function, args1: Array<Value>, args2: Array<Value>) -> HostResult {
		var al = args1.length, args = Array<Value>.new(al + args2.length);
		for (i < al) args[i] = args1[i];
		for (i < args2.length) args[i + al] = args2[i];
		return HostResult.TailCall(f, args);
	}
	def matchRtt(obj: Object, rtt: HeapTypeDecl) -> bool {
		match (obj) {
			x: HeapObject => return ValueTypes.isAssignableHeap(x.decl, rtt);
			x: WasmFunction => return ValueTypes.isAssignableHeap(x.sig, rtt);
		}
		return false;
	}
	def doCast(instance: Instance, nullable: bool, htcode: int, val: Value) -> bool {
		match (htcode) {
			BpHeapTypeCode.FUNC.val => return checkRef(nullable, Function.?, val);
			BpHeapTypeCode.EXTERN.val => return checkI31OrRef(nullable, Object.?, val);
			BpHeapTypeCode.ARRAY.val => return checkRef(nullable, HeapArray.?, val);
			BpHeapTypeCode.STRUCT.val => return checkRef(nullable, HeapStruct.?, val);
			BpHeapTypeCode.ANY.val => return checkI31OrRef(nullable, Object.?, val);
			BpHeapTypeCode.EQ.val => return checkI31OrRef(nullable, HeapObject.?, val);
			BpHeapTypeCode.I31.val => return checkI31OrRef(nullable, isFalse, val);
			BpHeapTypeCode.NOFUNC.val, // fallthru
			BpHeapTypeCode.NOEXTERN.val, // falltrhu
			BpHeapTypeCode.NONE.val => return checkRef(nullable, isFalse, val);
			_ => {
				if (!Value.Ref.?(val)) return false;
				var obj = Value.Ref.!(val).val;
				if (obj == null) return nullable;
				var index = u32.!(htcode); // XXX: make this a runtime error?
				var rtt = instance.heaptypes[index];
				return matchRtt(obj, rtt);
			}
		}
	}
	// Casts a Wasm value to the host object type {T}, if the value represents
	// an externref.
	def doHostCast<T>(nullable: bool, val: Value) -> MaybeTrap<T> {
		var d: T;
		match (val) {
			Ref(obj) => {
				if (obj == null) {
					if (nullable) return MaybeTrap(d, TrapReason.NONE);
					return MaybeTrap(d, TrapReason.NULL_DEREF);
				}
				if (T.?(obj)) return MaybeTrap(T.!(obj), TrapReason.NONE);
			}
			_ => ;
		}
		return MaybeTrap(d, TrapReason.FAILED_CAST);
	}
	private def isFalse(obj: Object) -> bool {
		return false;
	}
	private def checkI31OrRef(nullable: bool, checkRef: Object -> bool, val: Value) -> bool {
		match (val) {
			I31 => return true;
			Ref(obj) => {
				if (obj == null) return nullable;
				return checkRef(obj);
			}
			_ => return false;
		}
	}
	private def checkRef(nullable: bool, q: Object -> bool, val: Value) -> bool {
		if (!Value.Ref.?(val)) return false;
		var obj = Value.Ref.!(val).val;
		if (obj == null) return nullable;
		return q(obj);
	}

	def copyElemsInto(dest: Array<Value>, instance: Instance, dst_offset: u32, elem: ElemDecl, src_offset: u32, size: u32) -> bool {
		if (elem == null) {
			if (ArrayUtil.boundsCheck(dest, dst_offset, 0, size) < 0) return false;
			if (ArrayUtil.boundsCheck<byte>(null, src_offset, 0, size) < 0) return false;
			return true;
		}
		match (elem.details) {
			FuncRefs(vals) => {
				return ArrayUtil.safeCopyF(dest, dst_offset, vals, src_offset, size, instance.getFunctionAsVal);
			}
			Exprs(vals) => {
				return ArrayUtil.safeCopyF(dest, dst_offset, vals, src_offset, size, instance.evalInitExpr);
			}
		}
	}
}

type Result {
	case Value(vals: Array<Value>) {}
	case Throw(thrown: Throwable) {}
	case Suspend(stack: WasmStack, exntag: Tag, valrefs: Range<Value>);

	def equals(that: Result) -> bool {
		if (this == that) return true;
		if (Result.Value.?(this) && Result.Value.?(that)) {
			var a = Result.Value.!(this).vals;
			var b = Result.Value.!(that).vals;
			if (a == b) return true;
			if (a == null) return false;
			if (b == null) return false;
			if (a.length != b.length) return false;
			for (i < a.length) if (a[i] != b[i]) return false;
			return true;
		}
		if (Result.Throw.?(this) && Result.Throw.?(that)) {
			var a = Result.Throw.!(this).thrown;
			var b = Result.Throw.!(that).thrown;
			if (a == b) return true;
			if (Trap.?(a) && Trap.?(b)) {
				return Trap.!(a).reason == Trap.!(b).reason;
			}
			if (Exception.?(a) && Exception.?(b)) {
				var xa = Exception.!(a);
				var xb = Exception.!(b);
				if (xa.tag.decl != xb.tag.decl) return false;
				return Arrays.equal(xa.vals, xb.vals);
			}
		}
		if (Result.Suspend.?(this) && Result.Suspend.?(that)) {
			var a = Result.Suspend.!(this);
			var b = Result.Suspend.!(that);
			if (a.stack != b.stack) return false;
			if (a.exntag != b.exntag) return false;
			if (a.valrefs.length != b.valrefs.length) return false;
			for (i < a.valrefs.length) if (a.valrefs[i] != b.valrefs[i]) return false;
			return true;
		}
		return false;
	}
	def render(buf: StringBuilder) -> StringBuilder {
		match (this) {
			Value(vals) => {
				if (vals == null) {
					buf.puts("<null>");
				} else if (vals.length == 1) {
					Values.render(vals[0], buf);
				} else {
					Values.renderVals(buf, vals);
				}
			}
			Throw(thrown) => thrown.render(buf);
			Suspend(stack, tag, valrefs) => {
				buf.put1("<suspend: tag=%q, vals=", tag.render);
				Values.renderVals(buf, valrefs);
				buf.puts(">");
			}
		}
		return buf;
	}
}

// The various ways user code can resume from a break, trap, or timeout
type Resumption {
	case Continue;
	case Trap(reason: TrapReason, trace: bool);
}

def defaultBreak() -> Resumption {
	return Resumption.Continue;
}
def defaultTrap(reason: TrapReason) -> Resumption {
	return Resumption.Trap(reason, false);
}

// The execution strategy controls how the engine executes Wasm bytecode, e.g. whether
// an interpreter or compiler(s) are used, and at what points. It interposes at a number
// of important events in the lifecycle of Wasm modules and instances and may or may not
// perform synchronous or asynchronous work.
class ExecutionStrategy {
	// Called upon selection of this strategy (e.g. by command-line flag).
	def onActivate() { }

	// Called during execution startup and exit.
	def onEngineStart() { }
	def onEngineExit(code: int) { }

	// Called if monitors are activated for the program.
	def onMonitorsStart() { }
	def onFuncProbeInsert1(module: Module, func: FuncDecl, offset: int, p: Probe) { }
	def onFuncProbeInsert2(module: Module, func: FuncDecl, offset: int, p: Probe) { }
	def onFuncProbeInsertN(module: Module, func: FuncDecl, offset: int, p: Probe) { }
	def onFuncProbeRemove(module: Module, func: FuncDecl, offset: int) { }
	def onMonitorsFinish(module: Module, err: ErrorGen) { }

	// Called during binary decoding of a module and relevant sections.
	// Note that code validation has not yet been performed.
	// Example usage: pre-allocate code space and start compiler threads.
	def onModuleStart(module: Module, size: u32) { }			// note: size == 0 if estimate only
	def onCodeSectionStart(module: Module, num_funcs: u32, size: u32) { }
	def onFuncBody(module: Module, func_index: u32, body: Range<byte>, err: ErrorGen) { }
	def onCodeSectionFinish(module: Module, num_funcs: u32, size: u32, err: ErrorGen) { }
	def onModuleFinish(module: Module, size: u32, err: ErrorGen) { }		// note: size is exact

	// Called when a function has its code validated.
	// Example usage: do synchronous (AOT) one-off compile.
	def onFuncValidationStart(module: Module, func: FuncDecl) { }
	def onFuncValidationFinish(module: Module, func: FuncDecl, err: ErrorGen) { }

	// Called when a module is instantiated.
	// Example usage: generate code specialized to import bindings.
	def onInstantiateStart(instantiator: Instantiator) { }
	def onInstantiateFinish(instantiator: Instantiator, err: ErrorGen) { }

	// Called before a test run of a function. (For testing only).
	def onTestRun(wf: WasmFunction, err: ErrorGen) { }

	// Called if a new function is created in a module.
	def onNewFunction(wf: WasmFunction, err: ErrorGen) { }

	// Called if a function is detected as "hot" at a particular location (e.g. loop header)
	// that probably merits on-stack-replacement.
	def onTierUp(wf: WasmFunction, pc: int) -> TargetOsrInfo {
		var d: TargetOsrInfo;
		return d; // default: no OSR occurred
	}

	// Call a function with arguments and return a result.
	// Example implementation: call into the interpreter.
	def call(func: Function, args: Range<Value>) -> Result;

	// Reset all internal state of execution tiers.
	def reset() { }
}

// Deals with command-line options for selecting the mode, printing help, etc.
component ExecuteOptions {
	private var modes: List<(string, ExecutionStrategy, string)>;
	def var default: ExecutionStrategy;

	// Used internally to register different execution strategies.
	def registerMode(name: string, tiering: ExecutionStrategy, help: string) -> ExecutionStrategy {
		modes = List.new((name, tiering, help), modes);
		return tiering;
	}
	// Used internally to register different execution strategies.
	def registerDefaultMode(name: string, tiering: ExecutionStrategy, help: string) -> ExecutionStrategy {
		registerMode(name, tiering, help);
		Execute.tiering = default = tiering;
		return tiering;
	}
	// Set the mode from a command-line option.
	def setMode(name: Range<byte>) -> bool {
		for (l = modes; l != null; l = l.tail) {
			var t = l.head;
			if (ArrayUtil.equal(t.0, name)) {
				t.1.onActivate();
				Execute.tiering = t.1;
				return true;
			}
		}
		return false;
	}
	// Print help for the supported modes.
	def printHelp() {
		var out = Trace.OUT, H = out.putsln;
		H("");
		H("This build of the Wizard Engine responds to the execution mode flag in the following ways.");
		H("");
		for (l = modes; l != null; l = l.tail) {
			var t = l.head;
			out.puts("    -mode=");
			out.puts(t.0);
			out.pad(' ', 32);
			if (t.1 == default) out.puts("(default) ");
			out.puts(t.2);
			out.outln();
		}

		H("");
	}
}